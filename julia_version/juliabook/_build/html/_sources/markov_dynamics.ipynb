{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "9f665a73",
   "metadata": {},
   "source": [
    "(Chapter 3: Markov Dynamics)=\n",
    "```{raw} jupyter\n",
    "<div id=\"qe-notebook-header\" style=\"text-align:right;\">\n",
    "        <a href=\"https://quantecon.org/\" title=\"quantecon.org\">\n",
    "                <img style=\"width:250px;display:inline;\" src=\"https://assets.quantecon.org/img/qe-menubar-logo.svg\" alt=\"QuantEcon\">\n",
    "        </a>\n",
    "</div>\n",
    "```\n",
    "# Chapter 3: Markov Dynamics\n",
    "\n",
    "\n",
    "```{contents} Contents\n",
    ":depth: 2\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4b3b0c63",
   "metadata": {
    "tags": [
     "remove-cell"
    ]
   },
   "outputs": [],
   "source": [
    "using Pkg;\n",
    "Pkg.activate(\"../\");\n",
    "\n",
    "using PyCall;\n",
    "pygui(:tk);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5d620b3f",
   "metadata": {},
   "source": [
    "#### inventory_sim.jl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bfa74f74",
   "metadata": {
    "tags": [
     "hide-input"
    ]
   },
   "outputs": [],
   "source": [
    "using Distributions, IterTools, QuantEcon\n",
    "\n",
    "function create_inventory_model(; S=100,  # Order size\n",
    "                                  s=10,   # Order threshold\n",
    "                                  p=0.4)  # Demand parameter\n",
    "    ϕ = Geometric(p)\n",
    "    h(x, d) = max(x - d, 0) + S*(x <= s)\n",
    "    return (; S, s, p, ϕ, h)\n",
    "end\n",
    "\n",
    "\"Simulate the inventory process.\"\n",
    "function sim_inventories(model; ts_length=200)\n",
    "    (; S, s, p, ϕ, h) = model\n",
    "    X = Vector{Int32}(undef, ts_length)\n",
    "    X[1] = S  # Initial condition\n",
    "    for t in 1:(ts_length-1)\n",
    "        X[t+1] = h(X[t], rand(ϕ))\n",
    "    end\n",
    "    return X\n",
    "end\n",
    "\n",
    "\"Compute the transition probabilities and state.\"\n",
    "function compute_mc(model; d_max=100)\n",
    "    (; S, s, p, ϕ, h) = model\n",
    "    n = S + s + 1  # Size of state space\n",
    "    state_vals = collect(0:(S + s))\n",
    "    P = Matrix{Float64}(undef, n, n)\n",
    "    for (i, j) in product(1:n, 1:n)\n",
    "        P[i, j] = sum((h(i-1, d) == j-1)*pdf(ϕ, d) for d in 0:d_max)\n",
    "    end\n",
    "    return MarkovChain(P, state_vals)\n",
    "end\n",
    "\n",
    "\"Compute the stationary distribution of the model.\"\n",
    "function compute_stationary_dist(model)\n",
    "    mc = compute_mc(model)\n",
    "    return mc.state_values, stationary_distributions(mc)[1]\n",
    "end\n",
    "\n",
    "\n",
    "# Plots\n",
    "\n",
    "using PyPlot\n",
    "using LaTeXStrings\n",
    "PyPlot.matplotlib[:rc](\"text\", usetex=true) # allow tex rendering\n",
    "\n",
    "\n",
    "function plot_ts(model; fontsize=16, \n",
    "                   figname=\"../figures/inventory_sim_1.pdf\",\n",
    "                   savefig=false)\n",
    "    (; S, s, p, ϕ, h) = model\n",
    "    X = sim_inventories(model)\n",
    "    fig, ax = plt.subplots(figsize=(9, 5.2))\n",
    "    ax.plot(X, label=L\"X_t\", lw=3, alpha=0.6)\n",
    "    ax.set_xlabel(L\"t\", fontsize=fontsize)\n",
    "    ax.set_ylabel(\"inventory\", fontsize=fontsize)\n",
    "    ax.legend(fontsize=fontsize, frameon=false)\n",
    "    ax.set_ylim(0, S + s + 20)\n",
    "\n",
    "    if savefig == true\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "function plot_hist(model; fontsize=16, \n",
    "                   figname=\"../figures/inventory_sim_2.pdf\",\n",
    "                   savefig=false)\n",
    "    (; S, s, p, ϕ, h) = model\n",
    "    state_values, ψ_star = compute_stationary_dist(model) \n",
    "    X = sim_inventories(model; ts_length=1_000_000)\n",
    "    histogram = [mean(X .== i) for i in state_values]\n",
    "\n",
    "    fig, ax = plt.subplots(figsize=(9, 5.2))\n",
    "    ax.plot(state_values, ψ_star, \"k-\",  lw=3, alpha=0.7, label=L\"\\psi^*\")\n",
    "    ax.bar(state_values, histogram, alpha=0.7, label=\"frequency\")\n",
    "    ax.set_xlabel(\"state\", fontsize=fontsize)\n",
    "\n",
    "    ax.legend(fontsize=fontsize, frameon=false)\n",
    "    ax.set_ylim(0, 0.015)\n",
    "\n",
    "    if savefig == true\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d279c668",
   "metadata": {},
   "outputs": [],
   "source": [
    "model = create_inventory_model()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6e25a250",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_ts(model; savefig=true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "21202637",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_hist(model; savefig=true)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c0764b2c",
   "metadata": {},
   "source": [
    "#### is_irreducible.jl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "956499b4",
   "metadata": {
    "tags": [
     "hide-input"
    ]
   },
   "outputs": [],
   "source": [
    "using QuantEcon\n",
    "P = [0.1 0.9;\n",
    "     0.0 1.0]\n",
    "mc = MarkovChain(P)\n",
    "print(is_irreducible(mc))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a5c557dc",
   "metadata": {},
   "source": [
    "#### laborer_sim.jl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "803311fa",
   "metadata": {
    "tags": [
     "hide-input"
    ]
   },
   "outputs": [],
   "source": [
    "function create_laborer_model(; α=0.3, β=0.2)\n",
    "    return (; α, β)\n",
    "end\n",
    "\n",
    "function laborer_update(x, model)  # update X from t to t+1\n",
    "    (; α, β) = model\n",
    "    if x == 1    \n",
    "        x′ = rand() < α ? 2 : 1\n",
    "    else \n",
    "        x′ = rand() < β ? 1 : 2\n",
    "    end\n",
    "    return x′\n",
    "end\n",
    "\n",
    "function sim_chain(k, p, model)\n",
    "    X = Array{Int32}(undef, k)\n",
    "    X[1] = rand() < p ? 1 : 2\n",
    "    for t in 1:(k-1)\n",
    "        X[t+1] = laborer_update(X[t], model)\n",
    "    end\n",
    "    return X\n",
    "end\n",
    "\n",
    "function test_convergence(; k=10_000_000, p=0.5)\n",
    "    model = create_laborer_model()\n",
    "    (; α, β) = model\n",
    "    ψ_star = (1/(α + β)) * [β α]\n",
    "\n",
    "    X = sim_chain(k, p, model)\n",
    "    ψ_e = (1/k) * [sum(X .== 1)  sum(X .== 2)]\n",
    "    error = maximum(abs.(ψ_star - ψ_e))\n",
    "    approx_equal = isapprox(ψ_star, ψ_e, rtol=0.01)\n",
    "    println(\"Sup norm deviation is $error\")\n",
    "    println(\"Approximate equality is $approx_equal\")\n",
    " end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "41c38868",
   "metadata": {},
   "source": [
    "#### markov_js.jl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ee63c094",
   "metadata": {
    "tags": [
     "hide-input"
    ]
   },
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Infinite-horizon job search with Markov wage draws.\n",
    "\n",
    "\"\"\"\n",
    "\n",
    "using QuantEcon, LinearAlgebra\n",
    "include(\"s_approx.jl\")\n",
    "\n",
    "\"Creates an instance of the job search model with Markov wages.\"\n",
    "function create_markov_js_model(;\n",
    "        n=200,       # wage grid size\n",
    "        ρ=0.9,       # wage persistence\n",
    "        ν=0.2,       # wage volatility\n",
    "        β=0.98,      # discount factor\n",
    "        c=1.0        # unemployment compensation\n",
    "    )\n",
    "    mc = tauchen(n, ρ, ν)\n",
    "    w_vals, P = exp.(mc.state_values), mc.p\n",
    "    return (; n, w_vals, P, β, c)\n",
    "end\n",
    "\n",
    "\" The Bellman operator Tv = max{e, c + β P v} with e(w) = w / (1-β).\"\n",
    "function T(v, model)\n",
    "    (; n, w_vals, P, β, c) = model\n",
    "    h = c .+ β * P * v\n",
    "    e = w_vals ./ (1 - β)\n",
    "    return max.(e, h)\n",
    "end\n",
    "\n",
    "\" Get a v-greedy policy.\"\n",
    "function get_greedy(v, model)\n",
    "    (; n, w_vals, P, β, c) = model\n",
    "    σ = w_vals / (1 - β) .>= c .+ β * P * v\n",
    "    return σ\n",
    "end\n",
    "\n",
    "\"Solve the infinite-horizon Markov job search model by VFI.\"\n",
    "function vfi(model) \n",
    "    v_init = zero(model.w_vals)  \n",
    "    v_star = successive_approx(v -> T(v, model), v_init)\n",
    "    σ_star = get_greedy(v_star, model)\n",
    "    return v_star, σ_star\n",
    "end\n",
    "\n",
    "        \n",
    "\n",
    "# == Policy iteration == #\n",
    "\n",
    "\"Get the value of policy σ.\"\n",
    "function get_value(σ, model)\n",
    "    (; n, w_vals, P, β, c) = model\n",
    "    e = w_vals ./ (1 - β)\n",
    "    K_σ = β .* (1 .- σ) .* P\n",
    "    r_σ = σ .* e .+ (1 .- σ) .* c\n",
    "    return (I - K_σ) \\ r_σ\n",
    "end\n",
    "\n",
    "\n",
    "    \n",
    "\"Howard policy iteration routine.\"\n",
    "function policy_iteration(model)\n",
    "    σ = Vector{Bool}(undef, model.n)\n",
    "    i, error = 0, 1.0\n",
    "    while error > 0\n",
    "        v_σ = get_value(σ, model)\n",
    "        σ_new = get_greedy(v_σ, model)\n",
    "        error = maximum(abs.(σ_new - σ))\n",
    "        σ = σ_new\n",
    "        i = i + 1\n",
    "        println(\"Concluded loop $i with error $error.\")\n",
    "    end\n",
    "    return σ\n",
    "end\n",
    "\n",
    "\n",
    "# == Plots == #\n",
    "\n",
    "using PyPlot\n",
    "using LaTeXStrings\n",
    "PyPlot.matplotlib[:rc](\"text\", usetex=true) # allow tex rendering\n",
    "fontsize=16\n",
    "\n",
    "default_model = create_markov_js_model()\n",
    "\n",
    "\n",
    "function plot_main(; model=default_model,\n",
    "                     method=\"vfi\", \n",
    "                     savefig=false, \n",
    "                     figname=\"../figures/markov_js_1.pdf\")\n",
    "    (; n, w_vals, P, β, c) = model\n",
    "\n",
    "\n",
    "    if method == \"vfi\"\n",
    "        v_star, σ_star = vfi(model)\n",
    "    else\n",
    "        σ_star = policy_iteration(model)\n",
    "        v_star = get_value(σ_star, model)\n",
    "    end\n",
    "\n",
    "    h_star = c .+ β * P * v_star\n",
    "    e = w_vals / (1 - β)\n",
    "\n",
    "    fig, ax = plt.subplots(figsize=(9, 5.2))\n",
    "    ax.plot(w_vals, h_star, lw=4, ls=\"--\", alpha=0.4, label=L\"h^*(w)\")\n",
    "    ax.plot(w_vals, e, lw=4, ls=\"--\", alpha=0.4, label=L\"w/(1-\\beta)\")\n",
    "    ax.plot(w_vals, max.(e, h_star), \"k-\", alpha=0.7, label=L\"v^*(w)\")\n",
    "    ax.legend(frameon=false, fontsize=fontsize)\n",
    "    ax.set_xlabel(L\"w\", fontsize=fontsize)\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9a15ba5b",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_main(savefig=true)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a797918f",
   "metadata": {},
   "source": [
    "#### markov_js_with_sep.jl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3f3b684c",
   "metadata": {
    "tags": [
     "hide-input"
    ]
   },
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Infinite-horizon job search with Markov wage draws and separation.\n",
    "\n",
    "\"\"\"\n",
    "\n",
    "include(\"s_approx.jl\")\n",
    "using QuantEcon, LinearAlgebra\n",
    "\n",
    "\"Creates an instance of the job search model with separation.\"\n",
    "function create_js_with_sep_model(;\n",
    "        n=200,          # wage grid size\n",
    "        ρ=0.9, ν=0.2,   # wage persistence and volatility\n",
    "        β=0.98, α=0.1,  # discount factor and separation rate\n",
    "        c=1.0)          # unemployment compensation\n",
    "    mc = tauchen(n, ρ, ν)\n",
    "    w_vals, P = exp.(mc.state_values), mc.p\n",
    "    return (; n, w_vals, P, β, c, α)\n",
    "end\n",
    "\n",
    "\" The Bellman operator for the value of being unemployed.\"\n",
    "function T(v, model)\n",
    "    (; n, w_vals, P, β, c, α) = model\n",
    "    d = 1 / (1 - β * (1 - α))\n",
    "    accept = d * (w_vals + α * β * P * v)\n",
    "    reject = c .+ β * P * v\n",
    "    return max.(accept, reject)\n",
    "end\n",
    "\n",
    "\" Get a v-greedy policy.\"\n",
    "function get_greedy(v, model)\n",
    "    (; n, w_vals, P, β, c, α) = model\n",
    "    d = 1 / (1 - β * (1 - α))\n",
    "    accept = d * (w_vals + α * β * P * v)\n",
    "    reject = c .+ β * P * v\n",
    "    σ = accept .>= reject\n",
    "    return σ\n",
    "end\n",
    "\n",
    "\"Solve by VFI.\"\n",
    "function vfi(model) \n",
    "    v_init = zero(model.w_vals)  \n",
    "    v_star = successive_approx(v -> T(v, model), v_init)\n",
    "    σ_star = get_greedy(v_star, model)\n",
    "    return v_star, σ_star\n",
    "end\n",
    "\n",
    "\n",
    "# == Plots == #\n",
    "\n",
    "using PyPlot\n",
    "using LaTeXStrings\n",
    "PyPlot.matplotlib[:rc](\"text\", usetex=true) # allow tex rendering\n",
    "fontsize=16\n",
    "\n",
    "default_model = create_js_with_sep_model()\n",
    "\n",
    "\n",
    "function plot_main(; model=default_model,\n",
    "                     method=\"vfi\", \n",
    "                     savefig=false, \n",
    "                     figname=\"../figures/markov_js_with_sep_1.pdf\")\n",
    "    (; n, w_vals, P, β, c, α) = model\n",
    "    v_star, σ_star = vfi(model)\n",
    "\n",
    "    d = 1 / (1 - β * (1 - α))\n",
    "    accept = d * (w_vals + α * β * P * v_star)\n",
    "    h_star = c .+ β * P * v_star\n",
    "\n",
    "    w_star = Inf\n",
    "    for (i, w) in enumerate(w_vals)\n",
    "        if accept[i] ≥ h_star[i]\n",
    "            w_star = w\n",
    "            break\n",
    "        end\n",
    "    end\n",
    "    @assert w_star < Inf \"Agent never accepts\"\n",
    "\n",
    "    fig, ax = plt.subplots(figsize=(9, 5.2))\n",
    "    ax.plot(w_vals, h_star, lw=4, ls=\"--\", alpha=0.4, label=\"continuation value\")\n",
    "    ax.plot(w_vals, accept, lw=4, ls=\"--\", alpha=0.4, label=\"stopping value\")\n",
    "    ax.plot(w_vals, v_star, \"k-\", alpha=0.7, label=L\"v_u^*(w)\")\n",
    "    ax.legend(frameon=false, fontsize=fontsize)\n",
    "    ax.set_xlabel(L\"w\", fontsize=fontsize)\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n",
    "\n",
    "function plot_w_stars(; α_vals=LinRange(0.0, 1.0, 10),\n",
    "                        savefig=false, \n",
    "                        figname=\"../figures/markov_js_with_sep_2.pdf\")\n",
    "\n",
    "    w_star_vec = similar(α_vals)\n",
    "    for (i_α, α) in enumerate(α_vals)\n",
    "        print(i_α, α)\n",
    "        model = create_js_with_sep_model(α=α)\n",
    "        (; n, w_vals, P, β, c, α) = model\n",
    "        v_star, σ_star = vfi(model)\n",
    "\n",
    "        d = 1 / (1 - β * (1 - α))\n",
    "        accept = d * (w_vals + α * β * P * v_star)\n",
    "        h_star = c .+ β * P * v_star\n",
    "\n",
    "        w_star = Inf\n",
    "        for (i_w, w) in enumerate(w_vals)\n",
    "            if accept[i_w] ≥ h_star[i_w]\n",
    "                w_star = w\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "        @assert w_star < Inf \"Agent never accepts\"\n",
    "        w_star_vec[i_α] = w_star\n",
    "    end\n",
    "\n",
    "    fig, ax = plt.subplots(figsize=(9, 5.2))\n",
    "    ax.plot(α_vals, w_star_vec, lw=2, alpha=0.6, label=\"reservation wage\")\n",
    "    ax.legend(frameon=false, fontsize=fontsize)\n",
    "    ax.set_xlabel(L\"\\alpha\", fontsize=fontsize)\n",
    "    ax.set_xlabel(L\"w\", fontsize=fontsize)\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7b06ac20",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_main(savefig=true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "11b83823",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_w_stars(savefig=true)"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "text_representation": {
    "extension": ".md",
    "format_name": "myst"
   }
  },
  "kernelspec": {
   "display_name": "Julia",
   "language": "julia",
   "name": "julia-1.9"
  },
  "source_map": [
   10,
   28,
   35,
   38,
   129,
   133,
   137,
   139,
   141,
   149,
   151,
   190,
   192,
   310,
   312,
   314,
   444,
   448
  ]
 },
 "nbformat": 4,
 "nbformat_minor": 5
}