{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "28d43d79",
   "metadata": {},
   "source": [
    "(Chapter 7: Nonlinear Valuation)=\n",
    "```{raw} jupyter\n",
    "<div id=\"qe-notebook-header\" style=\"text-align:right;\">\n",
    "        <a href=\"https://quantecon.org/\" title=\"quantecon.org\">\n",
    "                <img style=\"width:250px;display:inline;\" src=\"https://assets.quantecon.org/img/qe-menubar-logo.svg\" alt=\"QuantEcon\">\n",
    "        </a>\n",
    "</div>\n",
    "```\n",
    "# Chapter 7: Nonlinear Valuation\n",
    "\n",
    "\n",
    "```{contents} Contents\n",
    ":depth: 2\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c445e40c",
   "metadata": {
    "tags": [
     "remove-cell"
    ]
   },
   "outputs": [],
   "source": [
    "using Pkg;\n",
    "Pkg.activate(\"./\");\n",
    "\n",
    "using PyCall;\n",
    "pygui(:tk);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2b9942f2",
   "metadata": {},
   "source": [
    "## rs_utility.jl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "033cfe6f",
   "metadata": {
    "tags": [
     "hide-input"
    ]
   },
   "outputs": [],
   "source": [
    "include(\"s_approx.jl\")\n",
    "using LinearAlgebra, QuantEcon\n",
    "\n",
    "function create_rs_utility_model(;\n",
    "        n=180,      # size of state space\n",
    "        β=0.95,     # time discount factor\n",
    "        ρ=0.96,     # correlation coef in AR(1)\n",
    "        σ=0.1,      # volatility\n",
    "        θ=-1.0)     # risk aversion\n",
    "    mc = tauchen(n, ρ, σ, 0, 10)  # n_std = 10\n",
    "    x_vals, P = mc.state_values, mc.p \n",
    "    r = x_vals      # special case u(c(x)) = x\n",
    "    return (; β, θ, ρ, σ, r, x_vals, P)\n",
    "end\n",
    "\n",
    "function K(v, model)\n",
    "    (; β, θ, ρ, σ, r, x_vals, P) = model\n",
    "    return r + (β/θ) * log.(P * (exp.(θ*v)))\n",
    "end\n",
    "\n",
    "function compute_rs_utility(model)\n",
    "    (; β, θ, ρ, σ, r, x_vals, P) = model\n",
    "    v_init = zeros(length(x_vals))\n",
    "    v_star = successive_approx(v -> K(v, model), \n",
    "                               v_init, tolerance=1e-10)\n",
    "    return v_star\n",
    "end\n",
    "\n",
    "\n",
    "# Plots\n",
    "\n",
    "using PyPlot\n",
    "using LaTeXStrings\n",
    "PyPlot.matplotlib[:rc](\"text\", usetex=true) # allow tex rendering\n",
    "fontsize=16\n",
    "\n",
    "function plot_v(; savefig=false, \n",
    "                  figname=\"figures/rs_utility_1.pdf\")\n",
    "\n",
    "    fig, ax = plt.subplots(figsize=(10, 5.2))\n",
    "    model = create_rs_utility_model()\n",
    "    (; β, θ, ρ, σ, r, x_vals, P) = model\n",
    "\n",
    "    a = 1/(1 - (ρ*β))\n",
    "    b = (β /(1 - β)) * (θ/2) * (a*σ)^2 \n",
    "    \n",
    "    v_star = compute_rs_utility(model)\n",
    "    v_star_a = a * x_vals .+ b\n",
    "    ax.plot(x_vals, v_star, lw=2, alpha=0.7, label=\"approximate fixed point\")\n",
    "    ax.plot(x_vals, v_star_a, \"k--\", lw=2, alpha=0.7, label=L\"v(x)=ax + b\")\n",
    "    ax.set_xlabel(L\"x\", fontsize=fontsize)\n",
    "\n",
    "    ax.legend(frameon=false, fontsize=fontsize, loc=\"upper left\")\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "function plot_multiple_v(; savefig=false, \n",
    "                  figname=\"figures/rs_utility_2.pdf\")\n",
    "\n",
    "    fig, ax = plt.subplots(figsize=(10, 5.2))\n",
    "    σ_vals = 0.05, 0.1\n",
    "\n",
    "    for σ in σ_vals\n",
    "        model = create_rs_utility_model(σ=σ)\n",
    "        (; β, θ, r, x_vals, P) = model\n",
    "        v_star = compute_rs_utility(model)\n",
    "        ax.plot(x_vals, v_star, lw=2, alpha=0.7, label=L\"\\sigma=\"*\"$σ\")\n",
    "        ax.set_xlabel(L\"x\", fontsize=fontsize)\n",
    "        ax.set_ylabel(L\"v(x)\", fontsize=fontsize)\n",
    "    end\n",
    "\n",
    "    ax.legend(frameon=false, fontsize=fontsize, loc=\"upper left\")\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8f9169cb",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_v(savefig=true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "831a7639",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_multiple_v(savefig=true)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fa86fccf",
   "metadata": {},
   "source": [
    "## ez_utility.jl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f8a212a4",
   "metadata": {
    "tags": [
     "hide-input"
    ]
   },
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Epstein--Zin utility: solving the recursion for a given consumption\n",
    "path.\n",
    "\n",
    "\"\"\"\n",
    "\n",
    "include(\"s_approx.jl\")\n",
    "using LinearAlgebra, QuantEcon\n",
    "\n",
    "function create_ez_utility_model(;\n",
    "        n=200,      # size of state space\n",
    "        ρ=0.96,     # correlation coef in AR(1)\n",
    "        σ=0.1,      # volatility\n",
    "        β=0.99,     # time discount factor\n",
    "        α=0.75,     # EIS parameter\n",
    "        γ=-2.0)     # risk aversion parameter\n",
    "\n",
    "    mc = tauchen(n, ρ, σ, 0, 5) \n",
    "    x_vals, P = mc.state_values, mc.p \n",
    "    c = exp.(x_vals)      \n",
    "\n",
    "    return (; β, ρ, σ, α, γ, c, x_vals, P)\n",
    "end\n",
    "\n",
    "function K(v, model)\n",
    "    (; β, ρ, σ, α, γ, c, x_vals, P) = model\n",
    "\n",
    "    R = (P * (v.^γ)).^(1/γ)\n",
    "    return ((1 - β) * c.^α + β * R.^α).^(1/α)\n",
    "end\n",
    "\n",
    "function compute_ez_utility(model)\n",
    "    v_init = ones(length(model.x_vals))\n",
    "    v_star = successive_approx(v -> K(v, model), \n",
    "                               v_init, \n",
    "                               tolerance=1e-10)\n",
    "    return v_star\n",
    "end\n",
    "\n",
    "\n",
    "# Plots\n",
    "\n",
    "using PyPlot\n",
    "using LaTeXStrings\n",
    "PyPlot.matplotlib[:rc](\"text\", usetex=true) # allow tex rendering\n",
    "fontsize=16\n",
    "\n",
    "function plot_convergence(; savefig=false, \n",
    "                  num_iter=100,\n",
    "                  figname=\"figures/ez_utility_c.pdf\")\n",
    "\n",
    "    fig, ax = plt.subplots(figsize=(10, 5.2))\n",
    "    model = create_ez_utility_model()\n",
    "    (; β, ρ, σ, α, γ, c, x_vals, P) = model\n",
    "\n",
    "\n",
    "    v_star = compute_ez_utility(model)\n",
    "    v = 0.1 * v_star\n",
    "    ax.plot(x_vals, v, lw=3, \"k-\", alpha=0.7, label=L\"v_0\")\n",
    "\n",
    "    greys = [string(g) for g in LinRange(0.0, 0.4, num_iter)]\n",
    "    greys = reverse(greys)\n",
    "\n",
    "    for (i, g) in enumerate(greys)\n",
    "        ax.plot(x_vals, v, \"k-\", color=g, lw=1, alpha=0.7)\n",
    "        for t in 1:20\n",
    "            v = K(v, model)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    v_star = compute_ez_utility(model)\n",
    "    ax.plot(x_vals, v_star, lw=3, alpha=0.7, label=L\"v^*\")\n",
    "    ax.set_xlabel(L\"x\", fontsize=fontsize)\n",
    "\n",
    "    ax.legend(frameon=false, fontsize=fontsize, loc=\"upper left\")\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "function plot_v(; savefig=false, \n",
    "                  figname=\"figures/ez_utility_1.pdf\")\n",
    "\n",
    "    fig, ax = plt.subplots(figsize=(10, 5.2))\n",
    "    model = create_ez_utility_model()\n",
    "    (; β, ρ, σ, α, γ, c, x_vals, P) = model\n",
    "    v_star = compute_ez_utility(model)\n",
    "    ax.plot(x_vals, v_star, lw=2, alpha=0.7, label=L\"v^*\")\n",
    "    ax.set_xlabel(L\"x\", fontsize=fontsize)\n",
    "\n",
    "    ax.legend(frameon=false, fontsize=fontsize, loc=\"upper left\")\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "function vary_gamma(; gamma_vals=[1.0, -8.0],\n",
    "                  savefig=false, \n",
    "                  figname=\"figures/ez_utility_2.pdf\")\n",
    "\n",
    "    fig, ax = plt.subplots(figsize=(10, 5.2))\n",
    "    \n",
    "    for γ in gamma_vals\n",
    "        model = create_ez_utility_model(γ=γ)\n",
    "        (; β, ρ, σ, α, γ, c, x_vals, P) = model\n",
    "        v_star = compute_ez_utility(model)\n",
    "        ax.plot(x_vals, v_star, lw=2, alpha=0.7, label=L\"\\gamma=\"*\"$γ\")\n",
    "        ax.set_xlabel(L\"x\", fontsize=fontsize)\n",
    "        ax.set_ylabel(L\"v(x)\", fontsize=fontsize)\n",
    "    end\n",
    "\n",
    "    ax.legend(frameon=false, fontsize=fontsize, loc=\"upper left\")\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "function vary_alpha(; alpha_vals=[0.5, 0.6],\n",
    "                  savefig=false, \n",
    "                  figname=\"figures/ez_utility_3.pdf\")\n",
    "\n",
    "    fig, ax = plt.subplots(figsize=(10, 5.2))\n",
    "    \n",
    "    for α in alpha_vals\n",
    "        model = create_ez_utility_model(α=α)\n",
    "        (; β, ρ, σ, α, γ, c, x_vals, P) = model\n",
    "        v_star = compute_ez_utility(model)\n",
    "        ax.plot(x_vals, v_star, lw=2, alpha=0.7, label=L\"\\alpha=\"*\"$α\")\n",
    "        ax.set_xlabel(L\"x\", fontsize=fontsize)\n",
    "        ax.set_ylabel(L\"v(x)\", fontsize=fontsize)\n",
    "    end\n",
    "\n",
    "    ax.legend(frameon=false, fontsize=fontsize, loc=\"upper left\")\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "35a0e49e",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_convergence(savefig=true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6965dad7",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_v(savefig=true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b69b943f",
   "metadata": {},
   "outputs": [],
   "source": [
    "vary_gamma(savefig=true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f5cbd930",
   "metadata": {},
   "outputs": [],
   "source": [
    "vary_alpha(savefig=true)"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "text_representation": {
    "extension": ".md",
    "format_name": "myst"
   }
  },
  "kernelspec": {
   "display_name": "Julia",
   "language": "julia",
   "name": "julia-1.9"
  },
  "source_map": [
   10,
   28,
   35,
   38,
   125,
   129,
   131,
   133,
   279,
   283,
   287,
   291
  ]
 },
 "nbformat": 4,
 "nbformat_minor": 5
}