{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "a45b2542",
   "metadata": {},
   "source": [
    "(Chapter 4: Optimal Stopping)=\n",
    "```{raw} jupyter\n",
    "<div id=\"qe-notebook-header\" style=\"text-align:right;\">\n",
    "        <a href=\"https://quantecon.org/\" title=\"quantecon.org\">\n",
    "                <img style=\"width:250px;display:inline;\" src=\"https://assets.quantecon.org/img/qe-menubar-logo.svg\" alt=\"QuantEcon\">\n",
    "        </a>\n",
    "</div>\n",
    "```\n",
    "# Chapter 4: Optimal Stopping\n",
    "\n",
    "\n",
    "```{contents} Contents\n",
    ":depth: 2\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cb26239c",
   "metadata": {
    "tags": [
     "remove-cell"
    ]
   },
   "outputs": [],
   "source": [
    "using Pkg;\n",
    "Pkg.activate(\"./\");\n",
    "\n",
    "using PyCall;\n",
    "pygui(:tk);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c00d8d2b",
   "metadata": {},
   "source": [
    "## firm_exit.jl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "33c01364",
   "metadata": {
    "tags": [
     "hide-input"
    ]
   },
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Firm valuation with exit option.\n",
    "\n",
    "\"\"\"\n",
    "\n",
    "using QuantEcon, LinearAlgebra\n",
    "include(\"s_approx.jl\")\n",
    "\n",
    "\"Creates an instance of the firm exit model.\"\n",
    "function create_exit_model(;\n",
    "        n=200,                  # productivity grid size\n",
    "        ρ=0.95, μ=0.1, ν=0.1,   # persistence, mean and volatility\n",
    "        β=0.98, s=100.0         # discount factor and scrap value\n",
    "    )\n",
    "    mc = tauchen(n, ρ, ν, μ)\n",
    "    z_vals, Q = mc.state_values, mc.p\n",
    "    return (; n, z_vals, Q, β, s)\n",
    "end\n",
    "\n",
    "\n",
    "\"Compute value of firm without exit option.\"\n",
    "function no_exit_value(model)\n",
    "    (; n, z_vals, Q, β, s) = model\n",
    "    return (I - β * Q) \\ z_vals\n",
    "end\n",
    "\n",
    "\" The Bellman operator Tv = max{s, π + β Q v}.\"\n",
    "function T(v, model)\n",
    "    (; n, z_vals, Q, β, s) = model\n",
    "    h = z_vals .+ β * Q * v\n",
    "    return max.(s, h)\n",
    "end\n",
    "\n",
    "\" Get a v-greedy policy.\"\n",
    "function get_greedy(v, model)\n",
    "    (; n, z_vals, Q, β, s) = model\n",
    "    σ = s .>= z_vals .+ β * Q * v\n",
    "    return σ\n",
    "end\n",
    "\n",
    "\"Solve by VFI.\"\n",
    "function vfi(model) \n",
    "    v_init = no_exit_value(model)\n",
    "    v_star = successive_approx(v -> T(v, model), v_init)\n",
    "    σ_star = get_greedy(v_star, model)\n",
    "    return v_star, σ_star\n",
    "end\n",
    "\n",
    "\n",
    "# Plots\n",
    "\n",
    "using PyPlot\n",
    "using LaTeXStrings\n",
    "PyPlot.matplotlib[:rc](\"text\", usetex=true) # allow tex rendering\n",
    "fontsize=16\n",
    "\n",
    "\n",
    "function plot_val(; savefig=false, \n",
    "                     figname=\"figures/firm_exit_1.pdf\")\n",
    "\n",
    "    fig, ax = plt.subplots(figsize=(9, 5.2))\n",
    "\n",
    "    model = create_exit_model()\n",
    "    (; n, z_vals, Q, β, s) = model\n",
    "\n",
    "    v_star, σ_star = vfi(model)\n",
    "    h = z_vals + β * Q * v_star\n",
    "\n",
    "    ax.plot(z_vals, h, \"-\", lw=3, alpha=0.6, label=L\"h^*\")\n",
    "    ax.plot(z_vals, s * ones(n), \"-\", lw=3, alpha=0.6, label=L\"s\")\n",
    "    ax.plot(z_vals, v_star, \"k--\", lw=1.5, alpha=0.8, label=L\"v^*\")\n",
    "\n",
    "    ax.legend(frameon=false, fontsize=fontsize)\n",
    "    ax.set_xlabel(L\"z\", fontsize=fontsize)\n",
    "\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "function plot_comparison(; savefig=false, \n",
    "                     figname=\"figures/firm_exit_2.pdf\")\n",
    "\n",
    "    fig, ax = plt.subplots(figsize=(9, 5.2))\n",
    "\n",
    "    model = create_exit_model()\n",
    "    (; n, z_vals, Q, β, s) = model\n",
    "\n",
    "    v_star, σ_star = vfi(model)\n",
    "    w = no_exit_value(model)\n",
    "\n",
    "    ax.plot(z_vals, v_star, \"k-\", lw=2, alpha=0.6, label=L\"v^*\")\n",
    "    ax.plot(z_vals, w, lw=2, alpha=0.6, label=\"no-exit value\")\n",
    "\n",
    "    ax.legend(frameon=false, fontsize=fontsize)\n",
    "    ax.set_xlabel(L\"z\", fontsize=fontsize)\n",
    "\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "711073fd",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_val(savefig=true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fe41f248",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_comparison(savefig=true)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "23157136",
   "metadata": {},
   "source": [
    "## american_option.jl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "02f850ed",
   "metadata": {
    "tags": [
     "hide-input"
    ]
   },
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Valuation for finite-horizon American call options in discrete time.\n",
    "\n",
    "\"\"\"\n",
    "\n",
    "include(\"s_approx.jl\")\n",
    "using QuantEcon, LinearAlgebra, IterTools\n",
    "\n",
    "\"Creates an instance of the option model with log S_t = Z_t + W_t.\"\n",
    "function create_american_option_model(;\n",
    "        n=100, μ=10.0,  # Markov state grid size and mean value\n",
    "        ρ=0.98, ν=0.2,  # persistence and volatility for Markov state\n",
    "        s=0.3,          # volatility parameter for W_t\n",
    "        r=0.01,         # interest rate \n",
    "        K=10.0, T=200)  # strike price and expiration date\n",
    "    t_vals = collect(1:T+1)\n",
    "    mc = tauchen(n, ρ, ν)\n",
    "    z_vals, Q = mc.state_values .+ μ, mc.p\n",
    "    w_vals, φ, β = [-s, s], [0.5, 0.5], 1 / (1 + r)\n",
    "    e(t, i_w, i_z) = (t ≤ T) * (z_vals[i_z] + w_vals[i_w] - K)\n",
    "    return (; t_vals, z_vals, w_vals, Q, φ, T, β, K, e)\n",
    "end\n",
    "\n",
    "\"The continuation value operator.\"\n",
    "function C(h, model)\n",
    "    (; t_vals, z_vals, w_vals, Q, φ, T, β, K, e) = model\n",
    "    Ch = similar(h)\n",
    "    z_idx, w_idx = eachindex(z_vals), eachindex(w_vals)\n",
    "    for (t, i_z) in product(t_vals, z_idx)\n",
    "        out = 0.0\n",
    "        for (i_w′, i_z′) in product(w_idx, z_idx)\n",
    "            t′ = min(t + 1, T + 1)\n",
    "            out += max(e(t′, i_w′, i_z′), h[t′, i_z′]) * \n",
    "                       Q[i_z, i_z′] * φ[i_w′]\n",
    "        end\n",
    "        Ch[t, i_z] = β * out\n",
    "    end\n",
    "    return Ch\n",
    "end\n",
    "\n",
    "\"Compute the continuation value function by successive approx.\"\n",
    "function compute_cvf(model) \n",
    "    h_init = zeros(length(model.t_vals), length(model.z_vals))\n",
    "    h_star = successive_approx(h -> C(h, model), h_init)\n",
    "    return h_star\n",
    "end\n",
    "\n",
    "\n",
    "# Plots\n",
    "\n",
    "using PyPlot\n",
    "using LaTeXStrings\n",
    "PyPlot.matplotlib[:rc](\"text\", usetex=true) # allow tex rendering\n",
    "fontsize=16\n",
    "\n",
    "\n",
    "function plot_contours(; savefig=false, \n",
    "                         figname=\"figures/american_option_1.pdf\")\n",
    "\n",
    "    model = create_american_option_model()\n",
    "    (; t_vals, z_vals, w_vals, Q, φ, T, β, K, e) = model\n",
    "    h_star = compute_cvf(model)\n",
    "\n",
    "    fig, axes = plt.subplots(3, 1, figsize=(7, 11))\n",
    "    z_idx, w_idx = eachindex(z_vals), eachindex(w_vals)\n",
    "    H = zeros(length(w_vals), length(z_vals))\n",
    "\n",
    "    for (ax_index, t) in zip(1:3, (1, 195, 199))\n",
    "\n",
    "        ax = axes[ax_index, 1]\n",
    "\n",
    "        for (i_w, i_z) in product(w_idx, z_idx)\n",
    "            H[i_w, i_z] = e(t, i_w, i_z) - h_star[t, i_z]\n",
    "        end\n",
    "\n",
    "        cs1 = ax.contourf(w_vals, z_vals, transpose(H), alpha=0.5)\n",
    "        ctr1 = ax.contour(w_vals, z_vals, transpose(H), levels=[0.0])\n",
    "        plt.clabel(ctr1, inline=1, fontsize=13)\n",
    "        plt.colorbar(cs1, ax=ax) #, format=\"%.6f\")\n",
    "\n",
    "        ax.set_title(L\"t=\" * \"$t\", fontsize=fontsize)\n",
    "        ax.set_xlabel(L\"w\", fontsize=fontsize)\n",
    "        ax.set_ylabel(L\"z\", fontsize=fontsize)\n",
    "\n",
    "    end\n",
    "\n",
    "    fig.tight_layout()\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "function plot_strike(; savefig=false, \n",
    "                       fontsize=12,\n",
    "                       figname=\"figures/american_option_2.pdf\")\n",
    "\n",
    "    model = create_american_option_model()\n",
    "    (; t_vals, z_vals, w_vals, Q, φ, T, β, K, e) = model\n",
    "    h_star = compute_cvf(model)\n",
    "\n",
    "    # Built Markov chains for simulation \n",
    "    z_mc = MarkovChain(Q, z_vals)\n",
    "    P_φ = zeros(length(w_vals), length(w_vals))\n",
    "    for i in eachindex(w_vals)  # Build IID chain\n",
    "        P_φ[i, :] = φ\n",
    "    end\n",
    "    w_mc = MarkovChain(P_φ, w_vals)\n",
    "\n",
    "\n",
    "    y_min = minimum(z_vals) + minimum(w_vals)\n",
    "    y_max = maximum(z_vals) + maximum(w_vals)\n",
    "    fig, axes = plt.subplots(3, 1, figsize=(7, 12))\n",
    "\n",
    "    for ax in axes\n",
    "\n",
    "        # Generate price series\n",
    "        z_draws = simulate_indices(z_mc, T, init=Int(length(z_vals) / 2 - 10))\n",
    "        w_draws = simulate_indices(w_mc, T)\n",
    "        s_vals = z_vals[z_draws] + w_vals[w_draws]\n",
    "\n",
    "        # Find the exercise date, if any.\n",
    "        exercise_date = T + 1\n",
    "        for t in 1:T\n",
    "            if e(t, w_draws[t], z_draws[t]) ≥ h_star[w_draws[t], z_draws[t]]\n",
    "                exercise_date = t\n",
    "            end\n",
    "        end\n",
    "\n",
    "        @assert exercise_date ≤ T \"Option not exercised.\"\n",
    "\n",
    "        # Plot\n",
    "        ax.set_ylim(y_min, y_max)\n",
    "        ax.set_xlim(1, T)\n",
    "        ax.fill_between(1:T, ones(T) * K, ones(T) * y_max, alpha=0.2)\n",
    "        ax.plot(1:T, s_vals, label=L\"S_t\")\n",
    "        ax.plot((exercise_date,), (s_vals[exercise_date]), \"ko\")\n",
    "        ax.vlines((exercise_date,), 0, (s_vals[exercise_date]), ls=\"--\", colors=\"black\")\n",
    "        ax.legend(loc=\"upper left\", fontsize=fontsize)\n",
    "        ax.text(-10, 11, \"in the money\", fontsize=fontsize, rotation=90)\n",
    "        ax.text(-10, 7.2, \"out of the money\", fontsize=fontsize, rotation=90)\n",
    "        ax.text(exercise_date-20, 6, #s_vals[exercise_date]+0.8, \n",
    "                \"exercise date\", fontsize=fontsize)\n",
    "        ax.set_xticks((1, T))\n",
    "        ax.set_yticks((y_min, y_max))\n",
    "    end\n",
    "\n",
    "    if savefig\n",
    "        fig.savefig(figname)\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d94c71f6",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_contours(savefig=true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "aedff230",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_strike(savefig=true)"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "text_representation": {
    "extension": ".md",
    "format_name": "myst"
   }
  },
  "kernelspec": {
   "display_name": "Julia",
   "language": "julia",
   "name": "julia-1.9"
  },
  "source_map": [
   10,
   28,
   35,
   38,
   147,
   151,
   153,
   155,
   311,
   315
  ]
 },
 "nbformat": 4,
 "nbformat_minor": 5
}